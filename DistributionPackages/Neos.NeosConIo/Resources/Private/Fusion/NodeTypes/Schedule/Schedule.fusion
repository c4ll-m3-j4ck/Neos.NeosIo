prototype(Neos.NeosConIo:Schedule) < prototype(Neos.Neos:ContentComponent) {
    scheduleDate = ${q(node).property('scheduleDate')}
    title = ${q(node).property('title')}

    // Neos.NeosConIo:Schedule.Talks needs the event set.
    @context.event = ${q(node).property('event')}

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        tags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Talk', node)}
            3 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Event', node)}
            4 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Room', node)}
            5 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Speaker', node)}
            6 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:BreakInSchedule', node)}
        }
    }

    renderer = afx`
        <div class="schedule" data-component="Schedule">
            <h3 class="conferenceDayTitle">{props.title}</h3>
            <div class="conferenceDaySubtitle">
                {Date.format(props.scheduleDate, 'd M Y')}
            </div>
            <div class="talks">
                <Neos.NeosConIo:Schedule.Talks day={props.scheduleDate} />
            </div>
        </div>
    `
}
