prototype(Neos.Presentation:Organism.Navigation) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = "Navigation"
        props {
            links = Neos.Fusion:DataStructure {
                0 {
                    label = "Product"
                    link = "#"
                    subLinks {
                        0 {
                            label = "First"
                            link = "#"
                        }
                        1 {
                            label = "Second"
                            link = "#"
                        }
                    }
                }
                1 {
                    label = "Cases"
                    link = "#"
                }
                2 {
                    label = "Community"
                    link = "#"
                }
                3 {
                    label = "Download"
                    link = "#"
                }
                4 {
                    label = "Blog"
                    link = "#"
                }
            }
        }
    }

    @propTypes {
        links = ${PropTypes.arrayOf(PropTypes.dataStructure({
            "label": PropTypes.string.required,
            "link": PropTypes.string.required,
            "subLinks": PropTypes.arrayOf(PropTypes.dataStructure({"label": PropTypes.string.required, "link": PropTypes.string.required}))
        }))}
    }

    renderer = afx`
        <nav class="bg-darkblue text-white">
            <ul class="flex justify-between items-center mx-auto max-w-7xl py-7">
                <div class="bg-accent-lightblue h-14 w-40 flex justify-center items-center">NEOS ICON</div>
                <div class="flex gap-10">
                    <Neos.Fusion:Loop items={props.links} itemName="link">
                        <li>
                            <a href={link.link}>{link.label} <span @if={link.subLinks && link.subLinks != []}>V</span></a>
                            <div @if={link.subLinks && link.subLinks != []}>
                                <ul>
                                    <Neos.Fusion:Loop items={link.subLinks} itemName="subLink">
                                        <li>
                                            <a href={subLink.link}>{subLink.label}</a>
                                        </li>
                                    </Neos.Fusion:Loop>
                                </ul>
                            </div>
                        </li>
                    </Neos.Fusion:Loop>
                </div>
                <div class="bg-green h-14 w-40 flex justify-center items-center">TRY NEOS | V</div>
            </ul>
        </nav>
    `
}
